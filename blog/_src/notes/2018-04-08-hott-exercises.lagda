---
layout: "post"
permalink: /hott-exercises/
title: "HoTT Exercises"
date: "2018-04-08"
latex: true
references: true
agda: true
categories: type-theory home
toc: true
linkify: true
gallery: true
---

This is a self-contained version of some solutions for HoTT-Book's exercises.
The idea is to unpackage all as long as possible to get a better understanding.
Many changes can appear running this experiment. Solutions are
type-checked as a whole using `Agda` `v2.5.3`.

## Requirements

`Agda` has a pragma to be compatible with HoTT:

\begin{code}
{-# OPTIONS --without-K #-}
\end{code}

Equality type  also called Identity type can be defined as follows.

\begin{code}
infix 4 _â‰¡_
data _â‰¡_ {a} {A : Set a} (x : A) : A â†’ Set a where
  refl : x â‰¡ x
\end{code}

Some functions to work with this type:

\begin{code}
sym : âˆ€ {i} {A : Set i} {x y : A}
    â†’ x â‰¡ y â†’ y â‰¡ x
sym refl = refl

_Â·_ : âˆ€ {i}{X : Set i}{x y z : X}
    â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
refl Â· p = p
trans = _Â·_

infixl 9 _Â·_

ap : âˆ€ {i j}{A : Set i}{B : Set j}{x y : A}
   â†’ (f : A â†’ B) â†’ x â‰¡ y â†’ f x â‰¡ f y
ap f refl = refl

apâ‚‚ : âˆ€ {i j} {A B : Set i}{C : Set j}
      {xâ‚€ xâ‚ : A}{yâ‚€ yâ‚ : B}
    â†’ (f : A â†’ B â†’ C) â†’ (xâ‚€ â‰¡ xâ‚) â†’ (yâ‚€ â‰¡ yâ‚)
    â†’ f xâ‚€ yâ‚€ â‰¡ f xâ‚ yâ‚
apâ‚‚ f refl refl = refl

subst : âˆ€ {i j} {A : Set i}{x y : A}
      â†’ (B : A â†’ Set j) â†’ x â‰¡ y
      â†’ B x â†’ B y
subst B refl = (Î» z â†’ z)

\end{code}

Path induction:

\begin{code}
J' : âˆ€ {i j}{X : Set i}{x : X}
   â†’ (P : (y : X) â†’ x â‰¡ y â†’ Set j)
   â†’ P x refl
   â†’ (y : X)
   â†’ (p : x â‰¡ y)
   â†’ P y p
J' P u y refl = u

J : âˆ€ {i j}{X : Set i}
  â†’ (P : (x y : X) â†’ x â‰¡ y â†’ Set j)
  â†’ ((x : X) â†’ P x x refl)
  â†’ (x y : X)
  â†’ (p : x â‰¡ y)
  â†’ P x y p
J P u x y p = J' (P x) (u x) y p
\end{code}

## Chapter 1

### Exercise 1.1

{: .exercise }
  Given functions $f : A \rightarrow B$ and $g : B \to C$, define
  their composite $g\circ f:A\to C$.
  Show that we have $h \circ (g\circ f) \equiv (h\circ g)\circ f$.

We define the composition operation in `Agda` as follows.

\begin{code}
_âˆ˜_ : âˆ€ {i j k} {A : Set i}{B : Set j}{C : Set k}
    â†’ (B â†’ C)
    â†’ (A â†’ B)
    â†’ A â†’ C
g âˆ˜ f = Î» x â†’ g (f x)
\end{code}

Then, the `âˆ˜-assoc` shows us that associativity of this composition holds.

\begin{code}
âˆ˜-assoc : âˆ€ {i j k l} {A : Set i}{B : Set j}{C : Set k}{D : Set l}
        â†’ (h : C â†’ D)(g : B â†’ C)(f : A â†’ B)
        â†’ h âˆ˜ (g âˆ˜ f) â‰¡ (h âˆ˜ g) âˆ˜ f
âˆ˜-assoc f g h = refl
\end{code}

### Exercise 1.2

{: .exercise }
  Derive the recursion principle for products $\rec_{A\times B}$
  using only the projections, and verify that the definitional equalities
  are valid. Do the same for $\Sigma$-types.

To solve this problem we need to know:

  - The recursion principle for Î£-types:

    {:.eq}
      $$ \rec_{ A \times B}
        : \prod\limits_{C : \U} (A \to B \to C) â†’ A \times B \to C.
      $$

  - The recursion principle for Î£-types:

    <p class="equation">
    $ \rec_{\sum\limits_{(x : A) } B(x)}
      : \prod\limits_{(C : U)} (\Pi_{(x : A)} B(x) \rightarrow C) \rightarrow
        \sum_{(x : A)} B(x) \rightarrow C
    $
    </p>

<div class="proof" id="proof-1.2">
Proof.<br/>
For products:<br/>
If we have the projections,
$\proj_1 : A \times B \to \mathsf{A}$ and $\proj_2 : A \times B \to \mathsf{B}$,
then $\rec_2$ is another inhabitant where

<p class="equation">
$
\begin{align*}
&\rec_1 : \prod\limits_{C : \U} (A \to B \to C) \to A \times B \to C\\
&\rec_1~C~g~c~:\equiv~g~(\proj_1 c,~\proj_2 c).
\end{align*}
$
</p>
By reflexivity, we prove the equality between $\rec_{ A \times B}$ and $\rec_1$.
<br/>
<br/>
For sums:<br/>
The projections are $\proj_1 : \sum_{x : A}  Bx \to \mathsf{A}$ and
$\proj_2 :  \prod {(p : \sum_{x : A}  Bx)} \to \mathsf{B} (\proj_1 p)$.<br/>
By using these projections, we got another recursor defined as follows:
<p class="equation">
$$
\begin{align*}
&\rec_2 : \prod\limits_{C : \U}  (\prod_{x : A} Bx \to C) \to \sum\limits_{x : A} B x \to C\\
&\rec_2~C~g~c~=~g~(\proj_1 c)~(\proj_2 c)
\end{align*}
$$
</p>

By reflexivity, we prove the equality between $\rec_{\sum\limits_{(x : A) } B(x)}$ and $\rec_2$.
</div>

Now, I'm gonna use the universe levels from Agda in
the following exercises, including this one:

\begin{code}
open import Agda.Primitive
  using (Level; _âŠ”_; lzero; lsuc)
\end{code}

Î£-type (sigma type) definition (see the definition without projections
[here](https://github.com/jonaprieto/hott-book/blob/master/other/prelim.agda#L20)):

\begin{code}
module Î£-Defâ‚ where

  record Î£ {a b} (A : Set a) (B : A â†’ Set b) : Set (a âŠ” b) where
    constructor _,_
    field
      projâ‚ : A
      projâ‚‚ : B projâ‚

  open Î£ public
-- _,_ : (projâ‚ : A) â†’ B projâ‚ â†’ Î£ A B.
\end{code}

Its recursor with a function $g : \prod_{(x : A)} B(x)\rightarrow C$
that we provide.

\begin{code}
module Î£-Recâ‚ {i j k}{A : Set i}{B : A â†’ Set j}{C : Set k}
             (g : (x : A) â†’ B x â†’ C) where

  open Î£-Defâ‚ using (Î£ ; projâ‚; projâ‚‚; _,_ )

  rec : Î£ A B â†’ C
  rec p = g (projâ‚ p) (projâ‚‚ p)

  rec-Î² : (x : A)(y : B x) â†’ rec (x , y) â‰¡ g x y
  rec-Î² x y = refl
\end{code}


On the other hand, the product type is just a particular case of the sigma type
when the codomain is not dependent, as we can see next by omitting the argument
in `(Î» _ â†’ B)`.

\begin{code}
module Ã—-Defâ‚ where
  open Î£-Defâ‚ public

  _Ã—_ : {l k : Level} (A : Set l) (B : Set k) â†’ Set (l âŠ” k)
  A Ã— B = Î£ A (Î» _ â†’ B)
\end{code}

Its recursor with a function $g : A \rightarrow B \rightarrow C$ that we provide.

\begin{code}
module Ã—-Recâ‚ {i j k}{A : Set i}{B : Set j}{C : Set k} (g : A â†’ B â†’ C) where

  open Ã—-Defâ‚ using (_Ã—_; projâ‚; projâ‚‚; _,_)

  rec : A Ã— B â†’ C
  rec p = g (projâ‚ p) (projâ‚‚ p)

  rec-Î² : (x : A)(y : B) â†’ rec (x , y) â‰¡ g x y
  rec-Î² x y = refl
\end{code}



### Exercise 1.3

<p class="exercise">
Derive the induction principle for products $\mathsf{ind}_{A\times B}$,
using only the projections and the propositional uniqueness principle
$\mathsf{uniq}_{A\times B}$.
Verify that the definitional equalities are valid.
</p>

To solve this problem, recall the uniqueness principle (Pp. 29. HoTT-Book)

- The **propositional uniqueness principle** says that
every element of $A\times B$ is equal to a pair.

<p class="equation">
  $ \mathsf{uniq}_{A\times B}
     : \prod_{(x : A \times B)} ((\proj_{1}(x) , \proj_{2}(x))
       \equiv_{A\times B} x).
  $
</p>

Product type definition using `data`:

\begin{code}
-- this would be trivial in agda due to definitional Î· for records
-- so Capriotti defined a product type without Î·:
module Ã—-Defâ‚‚ where

  data _Ã—_ {i j}(A : Set i)(B : Set j) : Set (i âŠ” j) where
    _,_ : A â†’ B â†’ A Ã— B

  projâ‚ : âˆ€ {i j}{A : Set i}{B : Set j}
        â†’ A Ã— B â†’ A
  projâ‚ (a , b) = a

  projâ‚‚ : âˆ€ {i j}{A : Set i}{B : Set j}
        â†’ A Ã— B â†’ B
  projâ‚‚ (a , b) = b
\end{code}

Projections and $\mathsf{uniq}_{A\times B}$:

\begin{code}
module Ã—-Funâ‚‚ {i j}{A : Set i}{B : Set j} where
  open Ã—-Defâ‚‚ using ( _Ã—_;_,_; projâ‚; projâ‚‚)

  -- propositional uniqueness principle:
  uppt : (x : A Ã— B) â†’ (projâ‚ x , projâ‚‚ x) â‰¡ x
  uppt (a , b) = refl

  -- (ap functions): (a)ction over a (p)ath
  ap-projâ‚ : {A B : Set}{x y : A Ã— B}
           â†’ (x â‰¡ y) â†’ projâ‚ x â‰¡ projâ‚ y
  ap-projâ‚ refl = refl

  ap-projâ‚‚ : {A B : Set}{x y : A Ã— B}
           â†’ (x â‰¡ y) â†’ projâ‚‚ x â‰¡ projâ‚‚ y
  ap-projâ‚‚ refl = refl

  pair= : âˆ€ {A B : Set} {x w : A} {y z : B}
        â†’ x â‰¡ w â†’ y â‰¡ z â†’ (x , y) â‰¡ (w , z)
  pair= refl refl = refl
\end{code}

The induction principle for the product type:

<p class="equation">
$\mathsf{ind}_{A\times B} : \prod\limits_{C : A \times B \to \U}
  \left( \prod\limits_{x:A}\ \prod\limits_{y:B}\ \,C( (x,y) ) \right)
  \to \prod\limits_{x:A \times B}\ \,C(x)
$
</p>

\begin{code}
module Ã—-Indâ‚‚ {i j}{A : Set i}{B : Set j} where
  open Ã—-Defâ‚‚ using (_Ã—_; _,_;projâ‚;projâ‚‚)
  open Ã—-Funâ‚‚ using (uppt)

  ind : âˆ€ {k}(C : A Ã— B â†’ Set k)
        â†’ ((x : A)(y : B) â†’ C (x , y))
        â†’ (x : A Ã— B) â†’ C x
  ind C g x = subst C (uppt x) (g (projâ‚ x) (projâ‚‚ x))

  ind-Î² : âˆ€ {k} (C : A Ã— B â†’ Set k)
          â†’ (g : (x : A)(y : B) â†’ C (x , y))
          â†’ (x : A)(y : B)
          â†’ ind C g (x , y) â‰¡ g x y
  ind-Î² C g x y = refl
\end{code}

<p class="exercise">
Generalize $\mathsf{uniq}_{A\times B}$ to Î£-types, and do the same for
$\Sigma$-types, i.e. show induction and verify that the definitional equality
is valid.
</p>

Î£-type definition using `data`:

\begin{code}
module Î£-Defâ‚‚ where

  data Î£ {i j}(A : Set i)(B : A â†’ Set j) : Set (i âŠ” j) where
    _,_ : (x : A) â†’ B x â†’ Î£ A B
\end{code}

Projections and uniqueness propositional principle:

\begin{code}
module Î£-Funâ‚‚ {i j } {A : Set i}{B : A â†’ Set j} where
  open Î£-Defâ‚‚ using (Î£; _,_ )

  projâ‚ : Î£ A B â†’ A
  projâ‚ (a , b) = a

  projâ‚‚ : (x : Î£ A B) â†’ B (projâ‚ x)
  projâ‚‚ (a , b) = b

  uppt : (x : Î£ A B) â†’ (projâ‚ x , projâ‚‚ x) â‰¡ x
  uppt (a , b) = refl
\end{code}

Its induction principle:

\begin{code}
module Î£-Indâ‚‚ {i j}{A : Set i}{B : A â†’ Set j} where
  open Î£-Defâ‚‚ public
  open Î£-Funâ‚‚ public

  ind : (C : Î£ A B â†’ Set (i âŠ” j))
        â†’ ((x : A)(y : B x) â†’ C (x , y))
        â†’ (x : Î£ A B) â†’ C x
  ind C g (a , b) = g a b

  ind-Î² : (C : Î£ A B â†’ Set (i âŠ” j))
          â†’ (g : (x : A)(y : B x) â†’ C (x , y))
          â†’ (x : A) (y : B x)
          â†’ (ind C g (x , y)) â‰¡ g x y
  ind-Î² C g x y = refl
\end{code}

### Exercise 1.4

<div class="exercise">
Assuming as given only the <em>iterator</em> for natural numbers
<p class="equation">
$\mathsf{ite} : \prod\limits_{C:\U} C \to (C \to C) \to \N \to C $
</p>
with the defining equations
<p class="equation">
$
\begin{align*}
\mathsf{ite}(C,c_0,c_s,0)               &\equiv c_0, \\
\mathsf{ite}(C,c_0,c_s,\mathsf{suc}(n)) &\equiv c_s(\mathsf{ite}(C,c_0,c_s,n)),
\end{align*}
$
</p>
derive a function having the type of the recursor $\rec$.
Show that the defining equations of the recursor hold propositionally for this
function, using the induction principle for $\N$.
</div>

To solve this problem, let us define the recursor and also the induction principle
for natural numbers. (See more details about these functions in
[Induction on Natural Numbers]({% post_url 2018-02-12-induction-on-natural-numbers %})).

\begin{code}
module â„•-Def where

  data â„• : Set where
    zero : â„•
    suc  : â„• â†’ â„•

module â„•-Rec where
  open â„•-Def
  rec : âˆ€ (C : Set) â†’ C â†’ (â„• â†’ C â†’ C) â†’ â„• â†’ C
  rec C câ‚€ câ‚› zero    = câ‚€
  rec C câ‚€ câ‚› (suc n) = câ‚› n (rec C câ‚€ câ‚› n)

module â„•-Ind where
  open â„•-Def
  ind : âˆ€ (C : â„• â†’ Set)
       â†’ (C zero)
       â†’ (âˆ€ (n : â„•) â†’ C n â†’ C (suc n))
       â†’ (âˆ€ (n : â„•) â†’ C n)
  ind C câ‚€ câ‚› zero    = câ‚€
  ind C câ‚€ câ‚› (suc n) = câ‚› n (ind C câ‚€ câ‚› n)
\end{code}

Now, we define the iterator following the equations above:

\begin{code}
module â„•-Fun where
  open â„•-Def using (â„•; zero; suc)
  open â„•-Rec using (rec)

  ite : âˆ€ (C : Set) â†’ C â†’ (C â†’ C) â†’ â„• â†’ C
  ite C câ‚€ câ‚› zero    = câ‚€
  ite C câ‚€ câ‚› (suc n) = câ‚› (ite C câ‚€ câ‚› n)
\end{code}

Then, we now can define the recursor using this `ite` by iterating over the type
`â„• Ã— C` (*reloading recursion*?).


\begin{code}
-- recursor
  open Ã—-Defâ‚‚ using (_Ã—_; projâ‚; projâ‚‚; _,_)

  recâ‚‚ : âˆ€ (C : Set) â†’ C â†’ (â„• â†’ C â†’ C) â†’ â„• â†’ (â„• Ã— C)
  recâ‚‚ C câ‚€ câ‚› n =
      (ite (â„• Ã— C)
           (zero , câ‚€)
           (Î» (p : â„• Ã— C) â†’ (suc (projâ‚ p) , câ‚› (projâ‚ p) (projâ‚‚ p)))
           n)
\end{code}

Now, we need to establish the propositional equality between these two
definitions of recursor, i.e, between `rec` and `recâ‚‚`. Let's use
induction to prove that.

\begin{code}
module ex1-4 where

  open â„•-Def using (â„•; zero; suc)
  open â„•-Rec using (rec)
  open â„•-Ind using (ind)
  open â„•-Fun using (ite; recâ‚‚)

  open Ã—-Defâ‚‚ using (_Ã—_; projâ‚; projâ‚‚; _,_)
  open Ã—-Funâ‚‚

  proof : (C : Set)(câ‚€ : C)(câ‚› : â„• â†’ C â†’ C)
        â†’ âˆ€ (n : â„•) â†’ recâ‚‚ C câ‚€ câ‚› n â‰¡ (n , rec C câ‚€ câ‚› n)

  proof C câ‚€ câ‚› zero    = refl
  proof C câ‚€ câ‚› (suc n) = pair= {A = â„•}{B = C} (ap suc v) (apâ‚‚ câ‚› v u)
    where
      v : projâ‚ {A = â„•}{B = C}
        (ite
            (â„• Ã— C)
            (zero , câ‚€)
            (Î» p â†’ suc (projâ‚ p) , câ‚› (projâ‚ p) (projâ‚‚ p)) n)
         â‰¡ n
      v = (ap-projâ‚ {A = â„•}{B = C} (proof C câ‚€ câ‚› n))

      u : projâ‚‚ {A = â„•}{B = C} (recâ‚‚ C câ‚€ câ‚› n) â‰¡ rec C câ‚€ câ‚› n
      u = ap-projâ‚‚  {A = â„•}{B = C} (proof C câ‚€ câ‚› n)
\end{code}


### Exercise 1.5

{: .exercise}

  Show that if we define $$A+B :\equiv \sum\limits_{(x : \two)} \rec_{\two} (\U, A , B, x),$$
  then we can give a definition of $\ind_{A + B}$ for which the
  definitional equalities holds.

To solve this problem, let me introduce the $\two$ type, which is, the
type only two constructors, the **Booleans**.

\begin{code}
module ğŸš-Defâ‚ where

  data ğŸš : Set where
    ğŸ˜ : ğŸš
    ğŸ™ : ğŸš
\end{code}


With the recursor:

\begin{code}
module ğŸš-Recâ‚ where

  open ğŸš-Defâ‚  using (ğŸ˜;ğŸ™;ğŸš)

  rec : âˆ€ {i} {C : Set i} (a : C) (b : C ) â†’ ğŸš â†’ C
  rec a b ğŸ˜ = a
  rec a b ğŸ™ = b
  -- rec is the same if_then_else
\end{code}

and its induction principle:

\begin{code}
module ğŸš-Indâ‚ where

  open ğŸš-Defâ‚ using (ğŸ˜;ğŸ™;ğŸš)

  ind : âˆ€ {i} {C : ğŸš â†’ Set i} â†’ C ğŸ˜ â†’ C ğŸ™ â†’ (c : ğŸš) â†’ C c
  ind câ‚€ câ‚ ğŸ˜ = câ‚€
  ind câ‚€ câ‚ ğŸ™ = câ‚
\end{code}

The we define the **coproduct** $A+B$ as follows:

\begin{code}
module +-Defâ‚ where

  open Î£-Defâ‚ using (Î£;_,_;projâ‚; projâ‚‚) public

  open ğŸš-Defâ‚ using (ğŸ˜;ğŸ™;ğŸš)
  open ğŸš-Recâ‚ using (rec)

  _+_ : âˆ€ {i} (A B : Set i) â†’ Set _
  A + B = Î£ ğŸš (rec A B) -- if it's ğŸ˜ return A otherwise returns B

  -- the tradional constructors
  inl : âˆ€ {i}{A B : Set i} â†’ A â†’ A + B
  inl a = (ğŸ˜ , a)

  inr : âˆ€ {i}{A B : Set i} â†’ B â†’ A + B
  inr b = (ğŸ™ , b)
\end{code}


Now, let's try to define the recursor for this coproduct, and later,
we'll try the dependent version of it to complete the exercise.

\begin{code}
module +-Recâ‚ where

  open +-Defâ‚ using (_+_; inl;inr;_,_)
  open ğŸš-Defâ‚ using (ğŸ˜;ğŸ™;ğŸš)

  rec : âˆ€ {i j} {A B : Set i} {C : Set j}
      â†’ (A â†’ C)
      â†’ (B â†’ C)
      â†’ A + B â†’ C
  rec f g (ğŸ˜ , a) = f a
  rec f g (ğŸ™ , b) = g b
\end{code}

Notice how the recursor of the coproduct matches with the elimination
rule of the disjunction conective also called *case analysis*. That's follows from the
[**propositions-as-types**](https://ncatlab.org/nlab/show/propositions+as+types).

![path](/assets/latexit-images/disj-elimination.png){: width="%50" }

Finally, the induction principle for the coproduct:

\begin{code}
module +-Indâ‚ where

  open +-Defâ‚ using (_+_; inl;inr; _,_)
  open ğŸš-Defâ‚ using (ğŸ˜;ğŸ™;ğŸš)

  ind : âˆ€ {i j} {A B : Set i} {C : A + B â†’ Set j}
      â†’ ((a : A) â†’ C (inl a))
      â†’ ((b : B) â†’ C (inr b))
      â†’ (p : A + B) â†’ C p
  ind f g (ğŸ˜ , a) = f a
  ind f g (ğŸ™ , b) = g b

  ind-Î²â‚ : âˆ€ {i j} {A B : Set i} {C : A + B â†’ Set j}
      â†’ (f : (a : A) â†’ C (inl a))
      â†’ (g : (b : B) â†’ C (inr b))
      â†’ (x : A) â†’ ind {C = C} f g (inl x) â‰¡ f x
  ind-Î²â‚ f g x = refl

  ind-Î²â‚‚ : âˆ€ {i j} {A B : Set i} {C : A + B â†’ Set j}
      â†’ (f : (a : A) â†’ C (inl a))
      â†’ (g : (b : B) â†’ C (inr b))
      â†’ (x : B) â†’ ind {C = C} f g (inr x) â‰¡ g x
  ind-Î²â‚‚ f g x = refl
\end{code}

### Exercise 1.6

<div class="exercise" id="ex-1.6">
Show that if we define $A \times B :â‰¡ \prod_{x:\mathbf{2}}\rec_{\mathbf{2}}\ (\U\, A\, B\, x)$,
then we can give a definition of $\mathsf{ind}_{A\times B}$ for which the definitional
equalities propositionally (i.e. using equality types).
</div>

\begin{code}
module Ã—-Defâ‚ƒ where
  open ğŸš-Defâ‚ using (ğŸ˜;ğŸ™;ğŸš) public
  open ğŸš-Recâ‚ using (rec)

  _Ã—_ : (A B : Set) â†’ Set
  A Ã— B = (x : ğŸš) â†’ rec A B x

  _,_ : âˆ€ {A B} â†’ A â†’ B â†’ A Ã— B
  (a , b) ğŸ˜ = a
  (a , b) ğŸ™ = b

  projâ‚ : âˆ€ {A B : Set} â†’ A Ã— B â†’ A
  projâ‚ x = x ğŸ˜

  projâ‚‚ : âˆ€ {A B : Set} â†’ A Ã— B â†’ B
  projâ‚‚ x = x ğŸ™

module Ã—-Funâ‚ƒ where
  open Ã—-Defâ‚ƒ

  pair= : âˆ€  {A}{B} {x y : A}{a b : B}
        â†’ x â‰¡ y â†’ a â‰¡ b â†’ (x , a) â‰¡ (y , b)
  pair= = apâ‚‚ _,_

  postulate
    Extensionality
      : {A : Set} {B : A â†’ Set} {f g : (x : A) â†’ B x}
      â†’ (âˆ€ x â†’ f x â‰¡ g x) â†’ f â‰¡ g

  uniq : âˆ€ {A B} â†’ (c : A Ã— B) â†’ (projâ‚ c , projâ‚‚ c) â‰¡ c
  uniq {A}{B} c = Extensionality helper
    where
      helper : âˆ€ (x : ğŸš) â†’ (projâ‚ c , projâ‚‚ c) x â‰¡ c x
      helper ğŸ˜ = refl
      helper ğŸ™ = refl

module Ã—-Indâ‚ƒ where
  open Ã—-Defâ‚ƒ
  open Ã—-Funâ‚ƒ

  ind : âˆ€ {A B} (C : A Ã— B â†’ Set)
      â†’ ((a : A)(b : B) â†’ C (a , b))
      â†’ (c : A Ã— B) â†’ C c
  ind {A}{B} C f c = subst C (uniq c) (f (c ğŸ˜) (c ğŸ™))

  ind-Î² : âˆ€ {A B} (C : A Ã— B â†’ Set)
        â†’ (g : (a : A)(b : B) â†’ C (a , b))
        â†’ ((a : A)(b : B) â†’ ind C g (a , b) â‰¡ g a b)
  ind-Î² {A}{B} C g a b = {! !}
    where
      helper :  (u : A Ã— B) â†’ (projâ‚ u , projâ‚‚ u) â‰¡ u
      helper u =  sym (uniq (projâ‚ u , projâ‚‚ u)) Â· uniq u

      uniq-compute : (x : A)(y : B) â†’ helper (x , y) â‰¡ refl
      uniq-compute x y = right-inverse (uniq (x , y))
        where
          right-inverse : âˆ€ {i}{X : Set i}{x y : X}
                        â†’ (p : x â‰¡ y)
                        â†’ (sym p) Â· p â‰¡ refl
          right-inverse refl = refl
\end{code}

### Exercise 1.8

<div class="exercise" id="exercise-1.8">

Define multiplication and exponentiation using $\rec{\N}$.
Verify that $(\N,+,0,\times,1)$ is a semiring using only
$\ind{\N}$.

</div>


<div class="proof" markdown="1">
Solution.<br/>
Operation definitions:

\begin{code}
module Ex1-8 where
  open â„•-Def
  open â„•-Rec

  _+_ : â„• â†’ â„• â†’ â„•
  zero    + n = n
  (suc n) + m = suc (n + m)

  multi : âˆ€ (n m : â„•) â†’ â„•
  multi = rec ((m : â„•) â†’ â„•) (Î» n â†’ zero) (Î» n f m â†’  m + f m)

  exp : âˆ€ (n m : â„•) â†’ â„•
  exp = rec ((m : â„•) â†’ â„•) (Î» n â†’ suc zero) (Î» n g m â†’ multi m (g m))
\end{code}

On the other hand, a **semiring** is a type? together with two binary operators S(+,*) satisfying the following conditions:

1. Additive associativity: $\prod_{a,b,c : S} (a+b)+c=a+(b+c)$.

2. Additive commutativity: $\prod_{a,b : S}  a+b=b+a$.

3. Multiplicative associativity: $$\prod_{a,b,c : S} (a*b)*c=a*(b*c)$$.

4. Left and right distributivity:

$$\prod_{a,b,c : S} a*(b+c)=(a*b)+(a*c)\text{  and  } \prod_{a,b,c : S} (b+c)*a=(b*a)+(c*a).$$

</div>

### Exercise 1.9

<div class="exercise" id="exercise-1.9">

Define the type family $\mathsf{Fin} : \N \to \U$ mentioned
at the end of Section 1.3, and the dependent function $\mathsf{fmax} :
\prod_{n:\N} \mathsf{Fin}(n+1)$ mentioned in Section 1.4.
</div>

\begin{code}
module Ex1-9 where

  open â„•-Def
  open Ex1-8 using (_+_)

  data _<_ : â„• â†’ â„• â†’ Set where
    z<n : (n : â„•) â†’ zero < n
    s<s : (n : â„•) (m : â„•) â†’ n < m â†’ suc n < suc m

  data _â‰¤_ : â„• â†’ â„• â†’ Set where
    zâ‰¤n : (n : â„•) â†’ zero â‰¤ n
    sâ‰¤s : (n : â„•) (m : â„•) â†’ n â‰¤ m â†’ suc n â‰¤ suc m

  open Î£-Defâ‚

  Fin : â„• â†’ Set
  Fin = Î» (n : â„•) â†’ (Î£ â„• (Î» m â†’ (suc m â‰¤ n)))

  fmax : (n : â„•) â†’ Fin (suc n)
  fmax zero    = (zero , sâ‰¤s zero zero (zâ‰¤n zero))
  fmax (suc n) = (suc n , sâ‰¤s (suc n) (suc n) (sâ‰¤s n n (helper n)))
    where
      helper : âˆ€ (n : â„•) â†’ n â‰¤ n
      helper zero    = zâ‰¤n zero
      helper (suc n) = sâ‰¤s n n (helper n)

  fmax-well : âˆ€ (n : â„•)
            â†’ (m : Fin (suc n))
            â†’ projâ‚ m â‰¤ projâ‚ (fmax n)
  fmax-well zero    (zero  , p)                    = zâ‰¤n zero
  fmax-well zero    (suc n , sâ‰¤s .(suc n) .zero p) = p
  fmax-well (suc n) (m     , sâ‰¤s .m .(suc n) p)    = p
\end{code}

### Exercise 1.11

<div class="exercise" id="exercise-1.11">
Show that for any type $A$, we have $\neg\neg\neg A \to \neg A$.
</div>

A propositional logic proof using [Agda-Prop](http://github.com/jonaprieto/agda-prop)
library:

\begin{code}
module Ex1-11 where
  open import Data.PropFormula 1

  proof : âˆ€ {Î“} {A}
        â†’ Î“ âŠ¢ Â¬ Â¬ Â¬ A âŠƒ Â¬ A
  proof {Î“}{A} =
    âŠƒ-intro
      (Â¬-intro
          (Â¬-elim
              (weaken A (assume {Î“ = Î“} (Â¬ (Â¬ (Â¬ A)))))
              (Â¬-intro
                (Â¬-elim
                  (assume {Î“ = Î“ , Â¬ Â¬ Â¬ A , A} (Â¬ A))
                  (weaken (Â¬ A) (assume {Î“ = Î“ , Â¬ Â¬ Â¬ A} A))))))
\end{code}

In type theory, the term is:

$$
Î» (x : ((A â†’ âŠ¥) â†’ âŠ¥) â†’ âŠ¥) (a : A) . x ((Î» h : A â†’ âŠ¥) . ha) :
((A â†’ âŠ¥) â†’ âŠ¥) â†’ âŠ¥) â†’ (A â†’ âŠ¥)
$$

### Exercise 1.16

<div class="exercise" id="exercise-1.16">
Show that addition of natural numbers is commutative:
$\prod\limits_{i,j : \N}\ (i + j = j + i).$
</div>

Hint: by induction twice.

\begin{code}
module Ex1-16 where
  open â„•-Def
  open â„•-Ind
  open Ex1-8 using (_+_)

  proof : (i : â„•) â†’ ((j : â„•) â†’ i + j â‰¡ j + i)
  proof =
    ind (Î» (i : â„•) â†’ ((j : â„•) â†’ i + j â‰¡ j + i))
      sproofâ‚
      sproofâ‚‚
    where
      sproofâ‚ : (j : â„•) â†’ j â‰¡ (j + zero)
      sproofâ‚ =
        ind (Î» (j : â„•) â†’ j â‰¡ (j + zero))
            refl
            (Î» n nâ‰¡n+zero â†’ ap suc nâ‰¡n+zero)

      sproofâ‚‚ : (n : â„•)
              â†’ ((j : â„•) â†’ (n + j) â‰¡ (j + n))
              â†’ ((j : â„•) â†’ suc (n + j) â‰¡ (j + suc n))
      sproofâ‚‚ n hypâ‚ =
          ind (Î» (j : â„•) â†’ suc (n + j) â‰¡ (j + suc n))
            (ap suc (sym (sproofâ‚ n)))
            (Î» m hypâ‚‚ â†’
                ap suc
                  (trans
                      (hypâ‚ (suc m))
                  (trans
                      (ap suc (sym (hypâ‚ m)))
                      hypâ‚‚)))
\end{code}

## Chapter 2

### Exercise 2.6

<div class="exercise" id="exercise-2.6">
Prove that if $p : x \equiv y$, then the function
$(p \cdot -) : (y \equiv z) \to (x \equiv z)$ is an equivalence.
</div>

![path-triangle](/assets/ipe-images/path-triangle.png){: width="%30" }

<div class="proof">
Solution. <br/>

To show the equivalence, it suffices to show a function
$g : x \equiv z \to y \equiv z$ such that we can prove
$ (p \cdot -) \circ g \sim \id_{x \equiv z}$ and
$ g \circ (p \cdot -) \sim \id_{y \equiv z}$.

Let's define the function $g$.
<p class="equation">
$
\begin{align*}
&g : x â‰¡ z â†’ y â‰¡ z\\
&g~m~=~\trans~(\sym~p)~m
\end{align*}
$
</p>
<br/>

($(p \cdot -) \circ g \sim \id_{x \equiv z}$):
Let be $m : x â‰¡ z$, we have,

<p class="equation">
$
\begin{align*}
(p \cdot -) \circ g (m) &= (p \cdot -)~(g~m)\\
                        &= (p \cdot -)~(\trans~(\sym~p)~m)\\
                        &= \trans~p~(\trans~(\sym~p)~m)\\
                        &= \trans~(\trans~p~(\sym~p))~m\\
                        &= \trans~\refl_{xâ‰¡z}~m\\
                        &= m
\end{align*}
$
</p>
<br/>
($ g \circ (p \cdot -) \sim \id_{y \equiv z}$):
Let be $n : y â‰¡ z$, we have,

<p class="equation">
$
\begin{align*}
g \circ (p \cdot -) n &= g (\trans~p~n)\\
                      &= (\trans~(\sym~p)~(\trans~p~n)\\
                      &= \trans~(\trans~(\sym~p)~p)~n\\
                      &= \trans~\refl_{y â‰¡ z}~n\\
                      &= n.
\end{align*}
$
</p>

</div>


### Exercise 2.10

<div class="exercise" id="exercise-2.10">
Prove that âˆ‘-types are associative, in that for any $A : \U$
and families $B : A  \to U$ and $C : \sum_{(x : A)} B(x) \to \U$,
we have
<p class="equation">
$\sum\limits_{(x : A)} \sum\limits_{(y : B(x))} C((x,y)) \simeq \sum\limits_{p : \sum_{x:A} B(x)} C(p)$.
</p>
</div>

<div class="proof" id="proof-2.10">
Solution.<br/>
We can prove that the following functions $f$ and $g$ are inverses.

<p class="equation">
$\sum\limits_{(x : A)} \sum\limits_{(y : B(x))} C((x,y)) \overset{f}{\underset{g}{\rightleftarrows}} \sum\limits_{p : \sum_{x:A} B(x)} C(p)$.
</p>
defined by $f(a,b,c) :\equiv ((a,b),c)$ and $g(z,c) :\equiv (\proj_1 z,\proj_{2} z, c)$.<br/>
Indeed,
<p class="equation">
$
\begin{align*}
(f \circ g) (z, c) &:\equiv f (g (z,c))\\
&:\equiv f\,(\proj_1 z,\proj_{2} z, c)\\
&:\equiv ((\proj_1 z,\proj_{2} z), c)
\end{align*}
$
</p>
</div>

\begin{code}
module Î£-Funâ‚ where
  open Î£-Defâ‚ using (projâ‚; projâ‚‚; _,_;Î£)

  f : âˆ€ {A : Set} {B : A â†’ Set} {C : Î£ A B â†’ Set}
    â†’ Î£ A (Î» a â†’ Î£ (B a) (Î» z â†’ C (a , z))) â†’ Î£ (Î£ A B) C
  f (a , (b , c)) = (a , b) , c

  g : âˆ€ {A : Set} {B : A â†’ Set} {C : Î£ A B â†’ Set}
     â†’ Î£ (Î£ A B) C â†’ Î£ A (Î» a â†’ Î£ (B a) (Î» z â†’ C (a , z)))
  g {A}{B}{C} (z , c) = (projâ‚ z , (projâ‚‚ z , c))

  f-g : âˆ€ {A : Set} {B : A â†’ Set} {C : Î£ A B â†’ Set}
        â†’ (x : Î£ (Î£ A B) C)
        â†’ f {A = A}{B = B}{C = C} (g x) â‰¡ x
  f-g x = refl

  g-f : âˆ€ {A : Set} {B : A â†’ Set} {C : Î£ A B â†’ Set}
        â†’ (x : Î£ A (Î» a â†’ Î£ (B a) (Î» b â†’ C (a , b))))
        â†’ g {A = A}{B = B}{C = C} (f x) â‰¡ x
  g-f x = refl
\end{code}


### Exercise 2.13

<div class="exercise" id="exercise-2.13">
Show that $(\two \simeq \two) \simeq \two$.
</div>

<div class="proof" markdown="1">
Solution.<br/>

The following text is from [Jdoughertyii](https://github.com/jdoughertyii/hott-exercises/blob/master/Ch02.v).<br/>
The result essentially says that $\two$ is equivalent to itself in two ways:
the identity provides one equivalence, and negation gives the other.  So we
first define these.  $\id_{\two}$ is its own quasi-inverse; we have
$\id_{\two} \circ \id_{\two} \equiv \id_{\two}$, so $\id_{\two}
\circ \id_{\two}
\sim \id_{\two}$ easily.  $\lnot$ is also its own quasi-inverse, since for
any $x$, $\lnot\lnot x = x$.
To show the result, we need to map $\id_{\two}$ and $\lnot$ onto $\two$ is
a quasi-invertible way.  But we need to define this map on all of $\two \simeq \two$.
So for any $h : \two\simeq \two$, let $f(h) = h(\zero_{\two})$, and define
$g : \two \to (\two \simeq \two)$ by

{: .equation}
  $$ g(\zero_{\two}) = \id_{\two}$$ and  $$g(\one_{\two}) = \lnot.$$

To show that these are quasi-inverses,
note first that whatever else is the case, an equivalence
$\two \simeq \two$ can't be a constant function, which we can prove by a case
analysis. Each of $f(\zero_{\two})$ and $f(\one_{\two})$ is in $\two$, so it is
either $\zero_{\two}$ or $\one_{\two}$.

So we have the cases:
 - $f(\zero_{\two}) = f(\one_{\two})$, in which case we can apply $f^{-1}$ to either side to get a contradiction, or
 - $f(\zero_{\two}) = \lnot f(\one_{\two})$. In which case we have the result
Showing that $f \circ g \sim \id_{\two}$ is easy, since we can do it by
cases.  We have

$$\begin{align*}
  f(g(\zero_{\two})) &= f(\id_{\two}) = \id_{\two}(\zero_{\two}) = \zero_{\two}
  \\
  f(g(\one_{\two})) &= f(\lnot) = \lnot \zero_{\two} = \one_{\two}
\end{align*}$$

For the other direction, suppose that $h : \two \simeq \two$ and that
function extensionality holds.
$h(\zero_{\two})$ is either $\zero_{\two}$ or $\one_{\two}$.  If the first, then
because $h$ isn't constant we have $h(\one_{\two}) = \lnot h(\zero_{\two}) =
\one_{\two}$, hence $h = \id_{\two}$.  Furthermore,

$$g(f(h)) = g(h(\zero_{\two})) = g(\zero_{\two}) = \id_\two = h$$

The same argument works for the other case. So $f$ is an equivalence, and
$(\two \simeq \two) \simeq \two$.

</div>

### Exercise 2.14

{: .exercise}

  Suppose we add to type theory [the equality reflection
  rule](https://www.youtube.com/watch?v=IlfQjWqrK6I) which says that if there is
  an element $p : \id(x,y) $, then in fact $ x :\equiv y$. Prove that
  for any $p : \id(x,x)$ we have $p \equiv \refl_x$.

  ![path](/assets/latexit-images/hott-ex2.14.png){: width="25%" }


{: .proof}
  Proof.<br/>
  We first fix the type of $$\refl_x$$ in order to apply effectively
  path induction.<br/>
  ![path](/assets/latexit-images/hott-ex2.14b.png){: width="100%" }
  Now, $$\refl_x$$ and $$p : x = y$$ in the formulation of path induction
  are well-typed. Therefore, by path induction, we show that
  $$\id(p,\mathsf{refl_{x}})$$. Let be $$x : A$$ and $$C :\equiv
  \prod_{y : A} \prod_{p : x \equiv y }\ p \equiv \refl_{x}$$.
  It sufficies to show and inhabitant of $$C(x, \refl_{x})$$, and this is
  $$\refl_{\refl_x} : \refl_x = \refl_x$$.
  By the equation reflection rule, since we have an inhabitant of $$p \equiv
  \refl_x$$, we conclude these terms are judgemental equal, i.e., $$p \equiv \refl_x$$.

### Exercise 2.17

<div class="exercise">
<ul>
  <li> Show that if $A \simeq A$  and $B \simeq B'$, then $(A\times B) \simeq (A'\times B')$. </li>
  <li> Give two proofs of this fact, one using univalence and one not using it, and show that the two proofs are equal.</li>
  <li> Formulate and prove analogous results for the other type formers: $\Sigma$, $\to$, $\Pi$, and $+$. </li>
</ul>
</div>

<div class="proof" markdown="1">
  Proof.<br/>
  - Assuming Univalence.
  ![path](/assets/latexit-images/hott-ex2.17.png){: width="50%" }
  - Without Univalence, it is easy to prove
  that the function $h$ defined as follows gives us the
  equivalence that we are looking for.
  $$\begin{align*}
  &h : (A\times B) \to (A'\times B')\\
  &h (a , b) = (f\, a, g\, b).
  \end{align*}$$
  With the inverse:
  $$\begin{align*}
  &h^{-1} : (A'\times B') \to (A\times B)\\
  &h^{-1} (a , b) = (f^{-1}\, a, g^{-1}\, b).
  \end{align*}$$
  To prove these proofs are equal it is suffices to
  show these underlying functions are equal.
</div>

## Chapter 3

To solve the following exercises, let us recall a few things:

- The *set* definition in HoTT:

A type $A$ is a **set** if for all $x, y : A$ and
all $p, q : x \equiv y$, we have $ p \equiv q$. In a proposition
we have

$$\mathsf{isSet}(A) :\equiv \prod\limits_{(x,y : A)}\prod\limits_{(p,q : x \equiv y)} (p \equiv q).$$

- The type for equivalence from $A$ to $B$

$$ (A \simeq B) :\equiv \sum\limits_{f : A \to B} \mathsf{isequiv}(f), $$
where
$$
\mathsf{isequiv(f)} :\equiv
  \left (\sum\limits_{g : B \to A} (f \circ g \sim \id_{B})\right) \times
  \left (\sum\limits_{h : B \to A} (h \circ f \sim \id_{A})\right)
$$

- The homotopy concept:

Let $f , g : \prod\limits_{(x:A)} P(x)$ be two sections of a
type family $P : A \to \U$. A **homotopy** from $f$ to $g$
is a dependent function of type

$$(f \sim g) :\equiv \prod\limits_{x : A} (f(x) \equiv g(x)).$$

### Exercise 3.1

{: .exercise"}
  Prove that if $A\simeq B$ and $A$ is a set, then so is $B$.


<div class="proof" id="proof-3.1" markdown="1" >
Proof.<br/>
Let be $x,y : B$ and $p : x \equiv_{B} y$ and $q : x \equiv_{B} y$.
We need to prove $ p \equiv q$.<br/>
Since $A\simeq B$ then there is a function $f : A \to B$ and some
$g : B \to A$ such that $f \circ g \sim id_{B}$.
Using this function $g$ over the path $p$ we get
$$\mathsf{ap}_{g} p : g x \equiv_{A} g y$$.

We do the same but this time over the path $q$, that is,
$$\mathsf{ap}_{g} q : g x \equiv_{A} g y$$.
Because of $A$ is a set, we have a new path called
$$m :\mathsf{ap}_{g} p \equiv_{gx \equiv_{A} gy} \mathsf{ap}_{g} q$$. <br/>

Now, an action over this path $m$
using the function $$\mathsf{ap}_{f} : x \equiv_{A} y \to f x \equiv_{B} f y$$
will give us

$$
\mathsf{ap}_{\mathsf{ap}_{f}} m :
(\mathsf{ap}_{f}) (\mathsf{ap}_{g} p) \equiv (\mathsf{ap}_{f})  (\mathsf{ap}_{g} q).
$$


By the lemmas in Chapter 2, we do the following reasoning:

$$
\begin{align*}
(\mathsf{ap}_{f}) (\mathsf{ap}_{g} p) \equiv (\mathsf{ap}_{f})  (\mathsf{ap}_{g} q) &=
  \mathsf{ap}_{f \circ g} p \equiv \mathsf{ap}_{f \circ g} q\\
  &=(\text{transporting by using} f \sim g)\\
  &=\mathsf{ap}_{\id_{B}} p \equiv \mathsf{ap}_{\id_{B}} q\\
  &=p \equiv q.
\end{align*}
$$

Then, we have the inhabitant, $$\mathsf{ap}_{\mathsf{ap}_{f}} m : p \equiv q$$.
</div>

### Exercise 3.2

{: .exercise}
  Prove that if $A$ and $B$ are sets, then so is $A+B$.

To solve this exercise, we should take a look of some results from Chapter 2,
Section 2.12.

<div class="proof" id="proof-3.2" markdown="1">
Proof.<br/>

Let be $x, y : A + B$, and paths $p : x \equiv y$, $q : x \equiv
y$. Let's get a path $p \equiv q$.  We proceed by case analysis. If
$x :\equiv \mathsf{inl} a$ and $y :\equiv \mathsf{inl} b$, for
some $a, b : A$ then $$\mathsf{ap}_{\mathsf{inl}^{-1}} p : a
\equiv_{A} b$$ and $$\mathsf{ap}_{\mathsf{inl}^{-1}} q : a \equiv_{A}
b$$.

Since $A$ is a set, there is a path between these last two terms,
this is, $$m : \mathsf{ap}_{\mathsf{inl}^{-1}} p \equiv
\mathsf{ap}_{\mathsf{inl}^{-1}} q$$.  Now, an action over this path
$$m$$ using the $$\mathsf{inl}$$ function give us:
$$\mathsf{ap}_{\mathsf{inl}} m : \mathsf{ap}_{\mathsf{inl}}
(\mathsf{ap}_{\mathsf{inl}^{-1}} p) \equiv \mathsf{ap}_{\mathsf{inl}}
(\mathsf{ap}_{\mathsf{inl}^{-1}} q)$$.<br/>

By path algebra we have $$\mathsf{ap}_{\mathsf{inl}}
m : p \equiv q$$ since $$\mathsf{ap}_{\mathsf{inl}}
(\mathsf{ap}_{\mathsf{inl}^{-1}} p) \equiv p$$.<br/> Following the
same reasoning, we prove the case $$x :\equiv \mathsf{inr} a$$ and $$y
:\equiv \mathsf{inr} b$$.  For the latest cases, $$x :\equiv
\mathsf{inl} a$$ and $$y :\equiv \mathsf{inr} b$$ and $$x :\equiv
\mathsf{inr} a$$ and $$y :\equiv \mathsf{inl} b$$, we use the
encode-decode method to derive a proof term for ğŸ˜ from
$p$ and $q$. Then, we may conclude anything we wish, that is, $p\equiv q$.
</div>

### Exercise 3.4

{: .exercise }
  Show that $A$ is a mere proposition if and only
  if $A\to A$ is contractible.

<div class="proof" markdown="1">
Proof. <br/>
- We must to show an inhabitant of the following type:

$$
\mathsf{isProp}(A) \to \sum\limits_{f : A \to A}\, \prod\limits_{g : A \to A } f = g.
$$

This is my proposed term:

$$
t :\equiv Î» \mathsf{Aprop} \to (\id_{A}, Î» (g : A \to A) \to \mathsf{funext} (\lambda (x : A) \to \mathsf{Aprop}~(\mathsf{id}~x)~(g~x)).
$$

- If $A â†’ A$ is contractible, all functions in this type are equal,
then in particular given $x, y : A$, the constant functions $f(w) \equiv x$
and $g(w) \equiv y$ are equal. Then, $\mathsf{happly} (f =_{A \to A} g, w) : x = y$.
Therefore $$A$$ is a mere proposition.

</div>

{::options parse_block_html="true" /}
<div class="references">

  - {% reference hottbook %}

  - {% reference Grayson2017 %}

  - {% reference Wadler2015PT %}

  - By Capriotti [HoTT-Exercises](https://github.com/pcapriotti/hott-exercises) and [Agda-Base](https://github.com/pcapriotti/agda-base/)

</div>
{::options parse_block_html="false" /}
